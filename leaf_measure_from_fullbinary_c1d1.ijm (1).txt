// Counts and measures objects from a single directory containing images and NO other files
// This version corresponds to baseline processing for seed counting
// Developed by Joseph Kim as an intern with the USDA:ARS, Maricopa, AZ, USA
// Last version: 4 August 2014
// Adapted for leaves by Addie Thompson, Purdue University, 8-17-15

// Requires imagej version 1.46c
requires("1.46c");

//user input to select source and dest directory
dir1 = getDirectory("Choose SOURCE Directory" );
dir2 = getDirectory("Choose DESTINATION Directory" );

// Processing begins here
macro "baseline seed counting" {

	// "Setbatchmode" makes process faster - does not display images 
	setBatchMode(true);

	//list of files
	list = getFileList(dir1);

	//run loop over files
	for (i=0; i<list.length; i++) {
	
		//displays progress of files 
		showProgress(i+1, list.length);
	
		open(dir1+list[i]);

		// Open file is processed

		//set color threshold
		setThreshold(0, 140);
		setOption("BlackBackground", false);
		run("Convert to Mask");

		//setTool("rectangle");
		makeRectangle(516, 48, 4062, 2676);

		// Analyze Particles counts seed and produces data and outlines	
		call("ij.plugin.filter.ParticleAnalyzer.setFontSize", 50);
	
		//choose desired measurements and decimal point
		run("Set Measurements...", "area center perimeter shape feret's display redirect=None decimal=3");
	
		//creates pixel to inch scale
		run("Set Scale...", "distance=86 known=1 pixel=1 unit=inch");

		//choose size and shape of particles to be detected and output information
		run("Analyze Particles...", "size=2-Infinity circularity=0.00-1.00 show=Outlines display summarize results");
	
		//saving seed outline picture
		saveAs("PNG", dir2+list[i]+"outline");
		
		close();
	}

	//save measurements
	selectWindow("Results");
	saveAs("Measurements", "" + dir2 + "leaf results.txt");

	//creates the summary
	macro "summary" {
	
		selectWindow("Summary");
		
		//save measurements
		saveAs("Measurements", "" + dir2 + "summary.txt");

		//display message box with OK button to close macro
		title = "Processing complete";
		msg = "Click \"OK\" to close macro.";
		waitForUser(title, msg);
		
	}

	// Closes any images left open to reduce memory usage
	// From http://rsb.info.nih.gov/ij/macros/Close_All_Windows.txt
	macro "Close All Windows" { 
      
		while (nImages>0) { 
        selectImage(nImages); 
          
		close(); 
		} 
  	}
}